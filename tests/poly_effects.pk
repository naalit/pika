# This makes sure explicit effect polymorphism works
eff Console of
    Print I32 : ()
end

fun poly [E] (f : I32 -> () with E) with E = do
    f 3
    f 4
end

fun handle (f : () -> () with Console) : () with IO = catch f () of
    () => ()
    eff (Console.Print i) k => do
        print i
        k ()
    end
end

eff Yield a of
    Y a : ()
end

type Coroutine a of
    Go a (() -> Coroutine a)
    Stop
where
    fun new [a] (gen : () -> () with Yield a) : Coroutine a = catch gen () of
        () => Stop
        eff (Yield.Y x) k => Go x k
    end

    ## Steps the coroutine, and if it yields a value, calls `run` on it. Otherwise it's done, and this function does nothing.
    ## Returns the new coroutine.
    fun step [a] (run : a -> () with IO) (x : Coroutine a) : Coroutine a with IO = case x of
        Stop => Stop
        Go a k => do
            run a
            k ()
        end
    end
end

# Expected output:
# 1
# 2
# 3
# 4
fun main () with IO = do
    # TODO require less annotations
    # TODO don't require parentheses around (Yield.Y)

    # First instantiate it with Console, and print it out
    handle (() => poly [Console] (i => Console.Print (i - 2)))
    # Then make it a coroutine, and print out the resultls again
    val k = Coroutine.new [I32] (() => poly (Yield.Y))
    val k = Coroutine.step print k
    val k = Coroutine.step print k
    val k = Coroutine.step print k
end