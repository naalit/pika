use std::str::FromStr;
use crate::common::*;
use crate::term::*;
use crate::binding::ParseDef;
use crate::error::{Spanned, Span};
use lalrpop_util::ParseError;
use crate::lexer::Tok;

grammar<'input>(input: &'input str);

extern {
    type Location = usize;
    type Error = Spanned<crate::lexer::LexError>;

    enum Tok<'input> {
        "fun" => Tok::Fun,            // "fun"
        "Type" => Tok::Type,           // "Type"
        "Int" => Tok::Int,            // "Int"
        "struct" => Tok::Struct,         // "struct"
        "the" => Tok::The,
        ":" => Tok::Colon,          // ":"
        "=>" => Tok::Arrow,          // "=>"
        "=" => Tok::Equals,         // "="
        "\n" => Tok::Newline,        // "\n"
        "(" => Tok::POpen,
        ")" => Tok::PClose,
        "," => Tok::Comma,
        INT_LIT => Tok::LitInt(<i32>),    // "12"
        NAME => Tok::Name(<&'input str>),   // "x"
        INDENT => Tok::Indent,
        DEDENT => Tok::Dedent,
    }
}

Lines<T>: Vec<T> = {
    "\n"* <v:(<T> "\n"+)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

pub Defs: Vec<ParseDef<'input>> = Lines<Def>;

Def: ParseDef<'input> = {
    <l:Spanned<Var>> ":" <t:STerm> "=" <r:STerm> => {
        let span = Span(t.span().0, r.span().1);
        ParseDef(l, Spanned::new(ParseTree::The(t, r), span))
    },
    <l:Spanned<Var>> ":" "=" <r:STerm> => ParseDef(l, r),
}

NotFun: ParseTree<'input> = {
    The,
    Binder,
    App,
}

STerm: Spanned<ParseTree<'input>> = Spanned<Term>;

Spanned<T>: Spanned<T> = {
    <l:@L> <t:T> <r:@R> => Spanned::new(t, Span(l, r)),
};

Term: ParseTree<'input> = {
    Fun,
    The,
    Binder,
    App,
}

App: ParseTree<'input> = {
    <f:Spanned<App>> <x:Spanned<Atom>> => ParseTree::App(f, x),
    Atom,
}

The: ParseTree<'input> = {
    "the" <t:Spanned<Atom>> <u:Spanned<Atom>> => ParseTree::The(t, u),
}

Binder: ParseTree<'input> = {
    <x:Var> ":" <t:STerm?> => ParseTree::Binder(x, t),
}

Binder2: Option<Spanned<ParseTree<'input>>> = {
    ":" <STerm?>,
}
/// a b c : T --> (a:T) (b:T) (c:T)
MultiBinder: Vec<Spanned<ParseTree<'input>>> = {
    <v:Spanned<Atom>+> <t:Binder2?> =>? if let Some(t) = t {
        let mut r = Vec::new();
        for i in v {
            let span = i.span();
            if let ParseTree::Var(i) = i.force_unwrap() {
                r.push(Spanned::new(ParseTree::Binder(i, t.clone()), span));
            } else {
                return Err(ParseError::User {
                    error: Spanned::new(crate::lexer::LexError::Other(": can only be used with binders".to_string()), span),
                });
            }
        }
        Ok(r)
    } else {
        Ok(v)
    },
}

SpanToo<T>: (T, Span) = {
    <l:@L> <t:T> <r:@R> => (t, Span(l, r)),
}

Fun: ParseTree<'input> = {
    "fun" <a:MultiBinder> "=>" <b:SpanToo<Term>> => {
        let (b, bs) = b;
        a.into_iter().rfold(b, |b, a| ParseTree::Fun(a, Spanned::new(b, bs)))
    },
}

Atom: ParseTree<'input> = {
    "(" ")" => ParseTree::Unit,
    "(" <a:STerm> "," <b:STerm> ")" => ParseTree::Pair(a, b),
    Builtin => ParseTree::Builtin(<>),
    "Type" => ParseTree::Type,
    Int => ParseTree::I32(<>),
    Var => ParseTree::Var(<>),
    "(" <Term> ")",
}

Builtin: Builtin = {
    "Int" => Builtin::Int,
}

Var: &'input str = {
    NAME => &<>,
}
Int: i32 = INT_LIT;
