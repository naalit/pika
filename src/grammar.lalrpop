use crate::query::{Compiler, Name};
use crate::error::*;
use crate::term::*;
use crate::lexer::*;
use lalrpop_util::ParseError;

// I'd like to only require &dyn Interner, but the calling code has a &dyn Compiler and Rust doesn't have trait object upcasting yet.
grammar<'input>(db: &dyn Compiler);

extern {
  type Location = usize;
  type Error = Spanned<LexError>;

  enum Tok<'input> {
    // Keywords
    "fun" => Tok::Fun,
    "val" => Tok::Val,
    "impl" => Tok::Impl,
    "do" => Tok::Do,
    "struct" => Tok::Struct,
    "sig" => Tok::Sig,
    "type" => Tok::Type,
    "case" => Tok::Case,
    "of" => Tok::Of,
    "end" => Tok::End,
    "Type" => Tok::TypeType,
    "with" => Tok::With,
    "pure" => Tok::Pure,
    "where" => Tok::Where,
    "raise" => Tok::Raise,
    "catch" => Tok::Catch,
    "and" => Tok::And,
    "or" => Tok::Or,
    "if" => Tok::If,
    "then" => Tok::Then,
    "else" => Tok::Else,
    "eff" => Tok::Eff,

    // Symbols the lexer recognizes as a "binary operator"
    ":" => Tok::Colon,
    "=" => Tok::Equals,
    "->" => Tok::Arrow,
    "=>" => Tok::WideArrow,
    "+" => Tok::Plus,
    "-" => Tok::Minus,
    "*" => Tok::Times,
    "/" => Tok::Div,
    "|" => Tok::Bar,
    "." => Tok::Dot,
    "," => Tok::Comma,
    "**" => Tok::Exp,
    "%" => Tok::Mod,
    "^^" => Tok::Xor,
    "<<" => Tok::LShift,
    ">>" => Tok::RShift,
    "&" => Tok::BitAnd,
    ">" => Tok::Gt,
    ">=" => Tok::GtE,
    "<" => Tok::Lt,
    "<=" => Tok::LtE,
    "==" => Tok::Eq,
    "!=" => Tok::NEq,
    "<|" => Tok::LPipe,
    "|>" => Tok::RPipe,

    // Tokens with a payload
    Lit => Tok::Lit(<Literal>),
    NAME => Tok::Name(<&'input str>),

    // Other tokens
    "?" => Tok::Question,
    "@" => Tok::At,
    "(" => Tok::POpen,
    ")" => Tok::PClose,
    "[" => Tok::SOpen,
    "]" => Tok::SClose,
    "{" => Tok::COpen,
    "}" => Tok::CClose,
    "\n" => Tok::Newline,
    OpNL => Tok::OpNewline,
    "\\" => Tok::Backslash,
  }
}

#[inline]
Sp<T>: Spanned<T> = {
  <l:@L> <v:T> <r:@R> => Spanned::new(v, Span(l, r)),
}

pub Defs: Vec<PreDefAn> = {
  "\n"* => Vec::new(),
  "\n"* <first:Def> <mut v:("\n"+ <Def>)*> "\n"* => {
    v.insert(0, first);
    v
  }
}

Def: PreDefAn = {
  <a:("@" "[" <Attr> "]" "\n"*)*> <d:Def_> => PreDefAn {
    attributes: a,
    inner: d,
  },
}

// Both definitions and declarations, since they're mostly allowed in the same contexts
// If declarations aren't allowed (e.g. in structs or the top level), it's nicer to use a handwritten error message for that case.
Def_: PreDef = {
  // -- definitions --

  "fun" "\n"* <name:Sp<Name>> <args:Args> <ty:(":" <Sp<Term>>)?> "=" <val:Sp<Term>> => {
    let ty = ty.unwrap_or_else(|| name.copy_span(Pre_::Hole(MetaSource::LocalType(*name))));
    PreDef::Fun(name, args, ty, val)
  },
  "val" "\n"* <name:Sp<Name>> <ty:(":" <Sp<Term>>)?> "=" <val:Sp<Term>> => {
    let ty = ty.unwrap_or_else(|| name.copy_span(Pre_::Hole(MetaSource::LocalType(*name))));
    PreDef::Val(name, ty, val)
  },
  "type" "\n"* <name:Sp<Name>> <args:Args?> "of" <cons:ConsBlock> => {
    PreDef::Type(name, false, args.unwrap_or_else(Vec::new), cons.0, cons.1)
  },
  "eff" "\n"* <name:Sp<Name>> <args:Args?> "of" <cons:ConsBlock> => {
    PreDef::Type(name, true, args.unwrap_or_else(Vec::new), cons.0, cons.1)
  },
  "impl" <name:("\n"* <Sp<Name>> ":")?> <ty:Sp<Term>> "=" <val:Sp<Term>> => {
    PreDef::Impl(name, ty, val)
  },
  Sp<Term> => PreDef::Expr(<>),

  // -- declarations --

  // TODO report an error if we use an arg without a type
  // although I guess elaboration will usually catch it
  "fun" "\n"* <name:Sp<Name>> <args:Args> ":" <ty:Sp<Term>> => PreDef::FunDec(name, args, ty),
  "val" "\n"* <name:Sp<Name>> ":" <ty:Sp<Term>> => PreDef::ValDec(name, ty),
};

// Top-level expression groups

Pre: Pre = Sp<Term>;

Term: Pre_ = {
  Expr,
  // We only allow RStarts at the end of an application if it's at the top level
  // Otherwise we get
  // a \x. x
  //   .something
  // which is ambiguous (`x.something` or `(a _).something`? Neither would be clear.)
  <f:Sp<Expr>> <x:Sp<RStart>> => {
    Pre_::App(Icit::Expl, f, x)
  },
}

Expr: Pre_ = {
  Op,
  <a:Sp<App>> "with" <b:Sp<App>> <mut rest:("," <Sp<App>>)*> => {
    let mut v = vec![b];
    v.append(&mut rest);
    Pre_::With(a, v)
  },
  <from:Sp<App>> "->" <to:Sp<Expr>> => Pre_::Fun(from, to),
  // For now `(a : Type) a -> a` is allowed, but we might eventually want to only allow `[a] a -> a`
  <args:PiArgs> <from:Sp<App>> "->" <to:Sp<Expr>> => {
    let span = Span(from.span().0, to.span().1);
    let fun = Spanned::new(Pre_::Fun(from, to), span);
    args.into_iter().rfold(fun, |to, (name, icit, ty)| {
      // TODO this span isn't perfect
      let span = Span(ty.span().0, to.span().1);
      Spanned::new(Pre_::Pi(name, icit, ty, to), span)
    }).unwrap()
  },
  <args:PiArgs> "->" <to:Sp<Expr>> => {
    args.into_iter().rfold(to, |to, (name, icit, ty)| {
      // TODO this span isn't perfect
      let span = Span(ty.span().0, to.span().1);
      Spanned::new(Pre_::Pi(name, icit, ty, to), span)
    }).unwrap()
  },
  RStart,
}

// These constructions are recognizable from the first token
// So they can be the last argument in an application
RStart: Pre_ = {
  "sig" Block<Def> => todo!("sigs"),
  "struct" <Block<Def>> => Pre_::Struct(<>),
  "do" <Block<Def>> => Pre_::Do(<>),
  "case" "\n"* <x:Sp<Expr>> "\n"* "of" <v:Block<Branch>> => Pre_::Case(x, v),
  "if" "\n"* <cond:Sp<Term>> "\n"* "then" "\n"* <yes:Sp<Term>> "\n"* "else" "\n"* <no:Sp<Expr>> => Pre_::If(cond, yes, no),
  // TODO: Is Expr okay here?
  // Basically we can't do multiline inside lambdas, we need
  // \x.(x
  //     .sort)
  // and this doesn't work:
  // \x.x
  //    .sort
  // unless we're in parentheses, which might be unintuitive
  "\\" "\n"* <args:Args> OpNL? "." <body:Sp<Expr>> => {
    args.into_iter().rfold(body, |body, (name, icit, ty)| {
      // TODO this span isn't perfect
      let span = Span(ty.span().0, body.span().1);
      Spanned::new(Pre_::Lam(name, icit, ty, body), span)
    }).unwrap()
  },
}

Branch: (Pre, Pre) = {
  <Sp<Pat>> "=>" <Sp<Term>>,
}
Pat: Pre_ = {
  PApp,
  <a:Sp<PApp>> "|" <b:Sp<Pat>> => Pre_::OrPat(a, b),
}
PApp: Pre_ = {
  PAtom,
  <f:Sp<PAtom>> <args:Sp<AppArg>+> => {
    args.into_iter().fold(f, |f, x| {
      let xspan = x.span();
      let span = Span(f.span().0, xspan.1);
      let (icit, x) = x.unwrap();
      let x = Spanned::new(x, xspan);
      Spanned::new(Pre_::App(icit, f, x), span)
    }).unwrap()
  },
  <f:Sp<PAtom>> "." <n:Sp<Name>> <v:Sp<AppArg>*> => {
    let v = v.into_iter().map(|x| {
      let span = x.span();
      let (icit, pre) = x.unwrap();
      (icit, Spanned::new(pre, span))
    }).collect();
    Pre_::Dot(f, n, v)
  },
  "eff" <a:Sp<PAtom>> <k:Sp<PAtom>> => Pre_::EffPat(a, k),
}
PAtom: Pre_ = {
  Name => if &<>.get(db) == "_" { Pre_::Hole(MetaSource::Hole) } else { Pre_::Var(<>) },
  Lit => Pre_::Lit(<>),
  "(" ")" => Pre_::Unit,
  "(" <Pat> ")",
}

App: Pre_ = {
  Atom,
  <f:Sp<Atom>> <args:Sp<AppArg>+> => {
    args.into_iter().fold(f, |f, x| {
      let xspan = x.span();
      let span = Span(f.span().0, xspan.1);
      let (icit, x) = x.unwrap();
      let x = Spanned::new(x, xspan);
      Spanned::new(Pre_::App(icit, f, x), span)
    }).unwrap()
  },
  <Sp<App>> "?" => Pre_::Catch(<>),
}

Atom: Pre_ = {
  "Type" => Pre_::Type,
  Name => if &<>.get(db) == "_" { Pre_::Hole(MetaSource::Hole) } else { Pre_::Var(<>) },
  Lit => Pre_::Lit(<>),
  "(" <Term> ")" => <>,
  "(" ")" => Pre_::Unit,
}


// Infix precedence handling.
// Precedence is only defined when it's clear.
// Like Haskell, all operators have lower precedence than function application.

LHS: Pre_ = {
  App,
  // Allow `a + b \n * x` = `(a + b) * x`
  <Op> OpNL,
}
RHS: Pre_ = {
  App,
}

// Standard precedence for arithmetic operators
Arith<S>: Pre_ = {
  Factor<S>,
  <a:Sp<Arith<S>>> <s:Sp<"+">> <b:Sp<Factor<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Add), a, b),
  <a:Sp<Arith<S>>> <s:Sp<"-">> <b:Sp<Factor<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Sub), a, b),
}
Factor<S>: Pre_ = {
  Exp<S>,
  <a:Sp<Factor<S>>> <s:Sp<"*">> <b:Sp<Exp<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Mul), a, b),
  <a:Sp<Factor<S>>> <s:Sp<"/">> <b:Sp<Exp<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Div), a, b),
  <a:Sp<Factor<S>>> <s:Sp<"%">> <b:Sp<Exp<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Mod), a, b),
}
Exp<S>: Pre_ = {
  S,
  <a:Sp<Exp<S>>> <s:Sp<"**">> <b:Sp<RHS>> => Pre_::BinOp(s.copy_span(BinOp::Exp), a, b),
}

// No precedence for bitwise operators, but all are left-associative.
Bitwise<S>: Pre_ = {
  // You can't use arithmetic with bitwise operators without parens, but we need a single chain.
  Arith<S>,
  Sp<S> (Sp<"&"> Sp<RHS>)+ => {
    let (s, v) = (<>);
    v.into_iter().fold(s, |s, (op, x)| {
      let span = Span(s.span().0, x.span().1);
      Spanned::new(Pre_::BinOp(op.copy_span(BinOp::BitAnd), s, x), span)
    }).unwrap()
  },
  Sp<S> (Sp<"|"> Sp<RHS>)+ => {
    let (s, v) = (<>);
    v.into_iter().fold(s, |s, (op, x)| {
      let span = Span(s.span().0, x.span().1);
      Spanned::new(Pre_::BinOp(op.copy_span(BinOp::BitOr), s, x), span)
    }).unwrap()
  },
  // Single "^" is ambiguous: exponentiation or xor?
  Sp<S> (Sp<"^^"> Sp<RHS>)+ => {
    let (s, v) = (<>);
    v.into_iter().fold(s, |s, (op, x)| {
      let span = Span(s.span().0, x.span().1);
      Spanned::new(Pre_::BinOp(op.copy_span(BinOp::BitXor), s, x), span)
    }).unwrap()
  },
  // I'd like << and >> for function composition, too - maybe the same operator, e.g. `impl [T P Q] ShiftRight (x: T -> P x) (y: [x] P x -> Q y) = ...`?
  // The alternative would be <<< and >>>.
  Sp<S> (Sp<"<<"> Sp<RHS>)+ => {
    let (s, v) = (<>);
    v.into_iter().fold(s, |s, (op, x)| {
      let span = Span(s.span().0, x.span().1);
      Spanned::new(Pre_::BinOp(op.copy_span(BinOp::BitShl), s, x), span)
    }).unwrap()
  },
  Sp<S> (Sp<">>"> Sp<RHS>)+ => {
    let (s, v) = (<>);
    v.into_iter().fold(s, |s, (op, x)| {
      let span = Span(s.span().0, x.span().1);
      Spanned::new(Pre_::BinOp(op.copy_span(BinOp::BitShr), s, x), span)
    }).unwrap()
  },
}

// No precedence or associativity within comparison operators.
// But, they're lower precedence than arithmetic or bitwise - `x + 2 == z * 3` works fine.
Comparison<S>: Pre_ = {
  Bitwise<S>,
  <a:Sp<Bitwise<S>>> <s:Sp<"==">> <b:Sp<Bitwise<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Eq), a, b),
  <a:Sp<Bitwise<S>>> <s:Sp<"!=">> <b:Sp<Bitwise<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::NEq), a, b),
  <a:Sp<Bitwise<S>>> <s:Sp<"<">> <b:Sp<Bitwise<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Lt), a, b),
  <a:Sp<Bitwise<S>>> <s:Sp<">">> <b:Sp<Bitwise<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Gt), a, b),
  <a:Sp<Bitwise<S>>> <s:Sp<"<=">> <b:Sp<Bitwise<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Leq), a, b),
  <a:Sp<Bitwise<S>>> <s:Sp<">=">> <b:Sp<Bitwise<RHS>>> => Pre_::BinOp(s.copy_span(BinOp::Geq), a, b),
}

// No precedence between `and` and `or`, but one can be repeated: `x and y and z` works but not `x and y or z`.
// These are lower precedence than Comparison, and are keywords since they don't actually behave like operators (lazy evaluation of the rhs).
Logical<S>: Pre_ = {
  Comparison<S>,
  <a:Sp<Comparison<S>>> <v:("and" <Sp<Comparison<RHS>>>)+> => {
    v.into_iter().fold(a, |a, b| {
      let span = Span(a.span().0, b.span().1);
      Spanned::new(Pre_::And(a, b), span)
    }).unwrap()
  },
  <a:Sp<Comparison<S>>> <v:("or" <Sp<Comparison<RHS>>>)+> => {
    v.into_iter().fold(a, |a, b| {
      let span = Span(a.span().0, b.span().1);
      Spanned::new(Pre_::Or(a, b), span)
    }).unwrap()
  },
}

// <| and |> have no precedence, but are left-associative: `x |> y |> z` = `z(y(x))`, `x <| y <| z` = `x y z`.
Op: Pre_ = {
  Logical<LHS>,
  <a:Sp<LHS>> <s:Sp<"<|">> <b:Sp<RHS>> => Pre_::BinOp(s.copy_span(BinOp::PipeL), a, b),
  <a:Sp<LHS>> <s:Sp<"|>">> <b:Sp<RHS>> => Pre_::BinOp(s.copy_span(BinOp::PipeR), a, b),
  // `a . b c d` = `b c d a`, where `b` is looked up in the context of `a`'s type.
  // Unless `a.b` is a field, in which case it's `(a.b) c d`.
  // `a b . c d` isn't allowed.
  // `a b \n . c d` is, though, like other operators.
  <f:Sp<Atom>> "." <n:Sp<Name>> <v:Sp<AppArg>*> => {
    let v = v.into_iter().map(|x| {
      let span = x.span();
      let (icit, pre) = x.unwrap();
      (icit, Spanned::new(pre, span))
    }).collect();
    Pre_::Dot(f, n, v)
  },
  <f:Sp<Op>> OpNL "." <n:Sp<Name>> <v:Sp<AppArg>*> => {
    let v = v.into_iter().map(|x| {
      let span = x.span();
      let (icit, pre) = x.unwrap();
      (icit, Spanned::new(pre, span))
    }).collect();
    Pre_::Dot(f, n, v)
  },
}


// Argument helpers

Args: Vec<(Name, Icit, PreTy)> = {
  (<ArgGroupN> "\n"*)+ => {
    <>.into_iter().flat_map(|(names, icit, ty)| if names.len() == 1 {
      vec![(names[0], icit, ty)]
    } else {
      names.into_iter().map(|name| (name, icit, ty.clone())).collect()
    }).collect()
  }
}

#[inline]
PiArgs: Vec<(Name, Icit, PreTy)> = {
  (<ArgGroup> "\n"*)+ => {
    <>.into_iter().flat_map(|(names, icit, ty)| if names.len() == 1 {
      vec![(names[0], icit, ty)]
    } else {
      names.into_iter().map(|name| (name, icit, ty.clone())).collect()
    }).collect()
  }
}

// ArgGroup is also used in Pi, where a bare name isn't allowed.
ArgGroupN: (Vec<Name>, Icit, PreTy) = {
  // TODO arbitrary patterns in function arguments, not just ()
  <l:@L> "(" ")" <r:@R> => (vec![db.intern_name("_".to_string())], Icit::Expl, Spanned::new(Pre_::Unit, Span(l, r))),
  <l:@L> <n:Name> <r:@R> => (vec![n], Icit::Expl, Spanned::new(Pre_::Hole(MetaSource::LocalType(n)), Span(l, r))),
  ArgGroup,
}

ArgGroup: (Vec<Name>, Icit, PreTy) = {
  // We're fine using the name "_", since it's impossible to access it (it turns into a hole)
  "(" ":" <ty:Sp<Expr>> ")" => (vec![db.intern_name("_".to_string())], Icit::Expl, ty),
  "(" <n:Name> ":" <ty:Sp<Expr>> ")" => (vec![n], Icit::Expl, ty),
  ImpArg => { let (v, t) = <>; (v, Icit::Impl, t) }
}

ImpArg: (Vec<Name>, PreTy) = {
  <l:@L> "[" <n:Name+> "]" <r:@R> => { let name = n[0]; (n, Spanned::new(Pre_::Hole(MetaSource::LocalType(name)), Span(l, r))) },
  "[" <n:Name+> ":" <ty:Sp<Expr>> "]" => (n, ty),
  "[" ":" <ty:Sp<Expr>> "]" => (vec![db.intern_name("_".to_string())], ty),
}

AppArg: (Icit, Pre_) = {
  "[" <Expr> "]" => (Icit::Impl, <>),
  Atom => (Icit::Expl, <>),
}

ConsArg: Vec<(Name, Icit, PreTy)> = {
  Sp<Atom> => vec![(db.intern_name("_".to_string()), Icit::Expl, <>)],
  ArgGroup => {
    let (names, icit, ty) = <>;
    if names.len() == 1 {
      vec![(names[0], icit, ty)]
    } else {
      names.into_iter().map(|name| (name, icit, ty.clone())).collect()
    }
  },
}

// Different language structures

Cons: PreCons = {
  <name:Sp<Name>> <args:ConsArg*> <ty:(":" <Sp<Expr>>)?> => PreCons(
    name,
    args.into_iter().flatten().collect(),
    ty,
  ),
}

Block<T>: Vec<T> = {
  "\n"* "end" => Vec::new(),
  "\n"* <(<T> "\n"+)+> "end",
  "\n"* <mut v:(<T> "\n"+)*> <last:T> "end" => {
    v.push(last);
    v
  },
}

ConsBlock: (Vec<PreCons>, Vec<PreDefAn>) = {
  "\n"* "end" => (Vec::new(), Vec::new()),
  "\n"* <(<Cons> "\n"+)+> "end" => (<>, Vec::new()),
  "\n"* <mut v:(<Cons> "\n"+)*> <last:Cons> "end" => {
    v.push(last);
    (v, Vec::new())
  },

  "\n"* "where" <Block<Def>> => (Vec::new(), <>),
  "\n"* <(<Cons> "\n"+)+> "where" <Block<Def>>,
  "\n"* <mut v:(<Cons> "\n"+)*> <last:Cons> "where" <v2:Block<Def>> => {
    v.push(last);
    (v, v2)
  },
}

// Basic tokens

Attr: Attribute = <l:@L> <n:NAME> <r:@R> =>? Attribute::parse(n).map_err(|x| ParseError::User {
  error: Spanned::new(LexError::Other(x), Span(l, r))
});
Name: Name = NAME => db.intern_name(String::from(<>));
